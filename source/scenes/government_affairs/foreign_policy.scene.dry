title: Foreign Policy
new-page: true
is-card: true
tags: govt_affairs, cabinet
on-arrival: month_actions += 1; foreign_policy_timer += 8
view-if: rep_majority = 1 and foreign_policy_timer <= 0
card-image: img/portraits/Seward.jpg
go-to: fp_menu
#subtitle: Diplomacy in a postwar world.

= Foreign Policy

What should our foreign policy priority be?

@fp_menu

- @pressure_mexico
- @pressure_mexico2
- @monroe_doctrine
- @monroe_doctrine2
- @british_reconciliation
- @british_reconciliation2
- @british_reconciliation3
- @recognize_haiti
- @continue_haiti
- @expel_fenians
- @support_fenians
- @easy_discard
- @cancel_advisor_action

@pressure_mexico
title: Pressure France to withdraw from Mexico.
view-if: french_mexico = 0
choose-if: budget >= 1
subtitle: -1 budget
unavailable-subtitle: We lack the budget to project power abroad.
on-arrival: europe_relation -= 2; budget -= 1; french_mexico = 1

Under Napoleon III, France has installed Maximilian as Emperor of Mexico. This violates the Monroe Doctrine, and now that the Civil War has ended, we are signaling our intention to uphold it.

We send diplomatic notes and troops to the border, warning France to withdraw. We are not prepared for war, but a strong signal may suffice.

@pressure_mexico2
title: Pressure France to withdraw from Mexico.
view-if: french_mexico = 1
choose-if: budget >= 1
subtitle: -1 budget
unavailable-subtitle: We lack the budget to sustain foreign pressure.
on-arrival: europe_relation -= 2; budget -= 1; french_mexico = 2; middle_class_rep += 3; radical_strength += 2; radical_dissent -= 2;

Facing mounting pressure and lacking international support, the French military evacuates Mexico. The Republican forces seize the opportunity to strike, defeating Maximilian's regime and executing the would-be Emperor.

Europe takes notice of the reassertion of American resolve in the Western Hemisphere.

@monroe_doctrine
title: Assert naval presence in the Caribbean.
view-if: caribbean_patrol = 0
choose-if: navy_strength >= 220 and budget >= 1
unavailable-subtitle: Our Navy isn't large enough to enforce the Monroe Doctrine.
on-arrival: caribbean_patrol += 1; europe_relation -= 1; moderate_strength += 1; economic_growth += 0.1; budget -= 1; navy_strength -= 5

The Caribbean is crawling with European ships and pirates. With our navy modernizing, we deploy warships to project power and enforce stability.

This reassures American businesses and reminds Europe that the Monroe Doctrine remains in effect.

@monroe_doctrine2
title: Continue to enforce a naval presence in the Caribbean.
view-if: caribbean_patrol = 1
choose-if: navy_strength >= 220 and budget >= 1
unavailable-subtitle: Our Navy isn't large enough to enforce the Monroe Doctrine.
on-arrival: {! Q.fight = Math.floor(Math.random() * 10)+1; !}
go-to: navy_fight if fight = 10;

The Caribbean is crawling with European ships and pirates. With our navy modernizing, we deploy warships to project power and enforce stability.

This reassures American businesses and reminds Europe that the Monroe Doctrine remains in effect.

@british_reconciliation
title: Improve relations with Britain.
view-if: treaty_Washington = 0
on-arrival: treaty_Washington += 1; economic_growth += 0.2; middle_class_rep += 3; moderate_strength += 2; moderate_dissent -= 2;

Britain’s stance during the Civil War — especially its early recognition of Confederate belligerency and shipbuilding — left a sour taste. But with peace restored, many in Washington favor reconciliation.

We seek talks on outstanding issues, including the *Alabama Claims* and Canadian border friction.

@british_reconciliation2
title: Improve relations with Britain.
view-if: treaty_Washington = 1
on-arrival: treaty_Washington += 1; economic_growth += 0.2; middle_class_rep += 3; moderate_strength += 2; moderate_dissent -= 2;

We continue to discuss the main issue for a great Anglo-sphere relationship, particularly the Alabama Claims and Border scrummages on the Canadian border.

@british_reconciliation3
title: Sign the Treaty of Washington
view-if: treaty_Washington = 2
choose-if: fenian = 0
unavailable-subtitle: Our inaction to the Fenian raids in Canada has made the British unwilling to sign the treaty.
on-arrival: treaty_Washington += 1; economic_growth += 0.2; middle_class_rep += 3; moderate_strength += 2; moderate_dissent -= 2; budget += 4;

We signed the *Treaty of Washington*, a landmark diplomatic success. The Alabama Claims are settled via arbitration, and Britain agrees to pay $15.5 million in gold to the United States as compensation for damages.

The money goes straight into the Treasury. Border disputes are resolved peacefully. Moderates hail it as a statesmanlike triumph, and even nationalists acknowledge the treaty upheld American honor — and paid for it, too.

@recognize_haiti
title: Normalize relations with Haiti.
view-if: haiti_support = 0
choose-if: conservative_strength < 30
unavailable-subtitle: The Conservative Republicans sway the party away from this
on-arrival: freedmen_rep += 3; radical_strength += 1; economic_growth += 0.1; europe_relation -= 1; haiti_support += 1; conservative_dissent -= 10

Though Haiti has been independent for decades, we have refused full diplomatic recognition, largely due to racial politics.

We now send an envoy to Port-au-Prince, signaling respect for Black self-government and rejecting European racial hierarchies.

@continue_haiti
title: Continue relations with Haiti.
view-if: haiti_support = 1
choose-if: conservative_strength < 30
unavailable-subtitle: The Conservative Republicans sway the party away from this
on-arrival: freedmen_rep += 3; radical_strength += 1; economic_growth += 0.1; europe_relation -= 1; conservative_dissent -= 5

We continue relations with Haiti, which is seen as a beacon of hope to the Freedmen of the party and the rest of the United States.

@expel_fenians
title: Crack down on the Fenian raids.
view-if: year >= 1866
choose-if: Us_police_strength > 10 and Us_police_loyalty >= 0.7
unavailable-subtitle: [? if Us_police_strength < 10: The Police force is too weak to forcefully stop the Fenian Brotherhood. ?][? if Us_police_loyalty < 0.7: The Police are too disloyal to effectively execute the job.?]
on-arrival: immigrant_laborers_rep -= 4; moderate_dissent -= 1; europe_relation += 1;

Irish nationalists, many Union veterans, have begun launching illegal raids into Canada from American soil. While popular among Irish Americans, these attacks threaten U.S.–British relations.

We move to shut them down, seizing arms caches and arresting ringleaders, to reassure London that Washington doesn’t endorse chaos on the border.

@support_fenians
title: Turn a blind eye to Fenian raids.
view-if: year >= 1866
on-arrival: immigrant_laborers_rep += 3; europe_relation -= 2; radical_strength += 1; conservative_dissent += 1; fenian += 1;

Irish-American war veterans organize cross-border raids into Canada, aiming to pressure Britain into freeing Ireland. Officially, we do nothing. Unofficially, we let it happen.

This earns us praise from Irish communities and condemnation from London.

@navy_fight

- @fight: Engage with the enemy fleet!
- @flee: Escape to prevent damage to our fleet.

@fight
on-arrival: {!
Q.fight2 = Math.floor(Math.random() * 10)+1;
!}
go-to: navy_fight_win if fight2 >= 5; navy_fight_lose if fight2 < 5

@navy_fight_win
on-arrival: navy_strength -= 12; middle_class_rep += 4

We have been attacked by a European fleet in the Caribbean! Our Navy was able to defeat the fleet and return home safely.

@navy_fight_lose
on-arrival: navy_strength -= 35; middle_class_rep -= 4; middle_class_dem += 4

We have been attacked by a European fleet in the Caribbean! Our Navy was defeated by the fleet and lost some ships, and the rest returned home safely.

@flee
on-arrival: navy_strength -= 5; middle_class_rep -= 2; middle_class_dem += 4

We escape from the enemy navy and we arrive home, but the populous says that we are cowards for not fighting. Also, Europe sees us as weak.

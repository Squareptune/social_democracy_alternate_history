title: Southern Occupation
on-arrival: month_actions += 1
view-if: rep_majority = 1 and occupation >= 1
card-image: img/Occupation.jpg
is-card: true
new-page: true

= Dealing with Southern Occupation

With Republican control of Congress, we can take a more assertive role in managing the Reconstruction of the South. Loyal officials and paramilitary forces can be empowered, hostile organizations suppressed, and the army deployed as needed.

// Limited options (occupation_score >= 1)
- @expand_union_league
- @reinstate_state_militias
- @demobilize_state_militias

// Extreme options (occupation_score >= 2)
- @form_republican_militias
- @deploy_federal_troops
- @crackdown_kkk
- @persecute_kkk

- @root: Take no action.

@form_republican_militias
title: Form Republican-aligned State Militias
view-if: state_militia_formed = 0
choose-if: occupation >= 2 and Us_police_loyalty >= 0.6
unavailable-subtitle: [? if occupation < 2: This action requires full federal occupation ?][? if Us_police_loyalty < 0.6: The police are too disloyal ?]
on-arrival: state_militia_formed = 1; state_militia_strength = 50; Union_League_strength += 30; Union_League_militancy += 0.05; freedmen_rep += 2; rural_dem += 2; dem_relation -= 5; Us_police_loyalty += 0.1

@expand_union_league
title: Expand the Union League
view-if: Union_League_strength < 100
choose-if: occupation >= 1 and resource >= 1
unavailable-subtitle: [? if resource < 1: We lack the funds ?]
on-arrival: Union_League_strength += 20; Union_League_militancy += 0.05; freedmen_rep += 1; middle_class_rep += 1; rural_dem += 1; dem_relation -= 3

@deploy_federal_troops
choose-if: occupation >= 2 and army_loyalty >= 0.6
unavailable-subtitle: [? if occupation < 2: This action requires full federal occupation ?][? if army_loyalty < 0.6: The army is too unreliable ?]
on-arrival: army_strength -= 100; army_militancy += 0.2; kkk_strength *= 0.8; kkk_militancy -= 0.02; Union_League_strength += 10; Union_League_militancy += 0.02; rural_dem += 2; unemployed_dem += 2; dem_relation -= 7; freedmen_rep += 1

@crackdown_kkk
title: Crackdown on the KKK
view-if: kkk_strength >= 1
choose-if: occupation >= 2 and Us_police_loyalty >= 0.6
unavailable-subtitle: [? if occupation < 2: This requires full occupation ?][? if Us_police_loyalty < 0.6: Police won't cooperate ?]
on-arrival: kkk_strength *= 0.7; kkk_militancy -= 0.05; Us_police_loyalty -= 0.05; army_militancy += 0.1; dem_relation -= 6; rural_dem += 2

@persecute_kkk
title: Further Persecute the Klan
view-if: kkk_strength < 1 and kkk_militancy > 0
choose-if: occupation >= 2 and judicial_reform >= 3 and Us_police_loyalty >= 0.7
unavailable-subtitle: [? if occupation < 2: Federal occupation is required ?][? if judicial_reform < 3 : Courts wonâ€™t prosecute ?][? if Us_police_loyalty < 0.7 : Police are too disloyal ?]
on-arrival: kkk_strength *= 0.6; kkk_militancy -= 0.05; Us_police_loyalty += 0.1; army_militancy += 0.1; dem_relation -= 4; rural_dem += 1

@reinstate_state_militias
view-if: state_militia_formed = 0
choose-if: occupation >= 1
unavailable-subtitle: [? if occupation < 1: We are not occupying the South ?]
on-arrival: state_militia_formed = 1; state_militia_strength = 40; Union_League_strength += 20; Us_police_strength += 10; freedmen_rep += 1; rural_dem += 2; dem_relation -= 3

@demobilize_state_militias
view-if: state_militia_formed = 1
choose-if: occupation >= 1
unavailable-subtitle: [? if occupation < 1: There is no federal presence to control the militias ?]
on-arrival: state_militia_formed = 0; state_militia_strength = 0; Union_League_strength -= 10; Us_police_strength -= 5; moderate_support += 1; dem_relation += 3; rural_dem -= 1; resource += 1
